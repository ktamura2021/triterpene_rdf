# Get species and family from uniprot
# saved as v20231129_db02_up_getfamily.tsv # v20231129
# saved as v20231224_db02_up_getfamily.tsv # v20231224
# saved as v20240207_db02_up_getfamily.tsv

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX up: <http://purl.uniprot.org/uniprot/>
PREFIX core: <http://purl.uniprot.org/core/>
PREFIX : <https://ktamura2021.github.io/triterpene_rdf/ontology.ttl#>

SELECT ?ID ?species ?family
WHERE {
  ?s  :ID ?ID ;
      :uniprot ?uniprot .
  SERVICE <https://rdfportal.org/sib/sparql> {
    ?uniprot core:organism ?taxonomy .
    ?taxonomy core:scientificName ?species .
    ?taxonomy rdfs:subClassOf ?ancestor .
    ?ancestor core:scientificName ?family .
    ?ancestor core:rank/rdfs:label ?rank .
  FILTER CONTAINS(?rank, "Family")
  }       
}