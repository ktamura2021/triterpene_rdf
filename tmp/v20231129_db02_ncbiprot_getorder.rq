# Get order from taxid linked to ncbiprot
# saved as v20231129_db02_ncbiprot_getorder.tsv

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX up: <http://purl.uniprot.org/uniprot/>
PREFIX core: <http://purl.uniprot.org/core/>
PREFIX : <https://raw.githubusercontent.com/ktamura2021/triterpenoid_rdf/main/ontology.ttl#>

SELECT ?ID ?order
WHERE {
  ?s  :ID ?ID ;
      :up_taxonomy ?up_taxonomy .
  SERVICE <https://rdfportal.org/sib/sparql> {
    ?up_taxonomy core:scientificName ?species .
    ?up_taxonomy rdfs:subClassOf ?ancestor .
    ?ancestor core:scientificName ?order .
    ?ancestor core:rank/rdfs:label ?rank .
  FILTER CONTAINS(?rank, "Order")
  }       
}